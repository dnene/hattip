ext {
    junit_version = '4.12'
}

buildscript {
    ext.kotlin_version = '1.0.1-2'
    ext.dokka_version = "0.9.7"
    ext.gradle_bintray_version = "1.6"
    repositories {
        mavenCentral()
        jcenter()
    }
    dependencies {
        classpath "org.jetbrains.dokka:dokka-gradle-plugin:${dokka_version}"
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        classpath "com.jfrog.bintray.gradle:gradle-bintray-plugin:${gradle_bintray_version}"
    }
}


apply plugin: 'java'
apply plugin: 'kotlin'
apply plugin: 'maven'
apply plugin: 'org.jetbrains.dokka'
apply plugin: 'com.jfrog.bintray'

group 'org.kotyle'
version '0.0.1'

repositories {
    mavenCentral()
    maven { url "https://dl.bintray.com/dnene/org.kotyle" }
    jcenter()
}

task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

artifacts {
    archives sourcesJar
    archives javadocJar
}

sourceCompatibility = 1.6

dokka {
    moduleName = 'hattip'
    outputFormat = 'javadoc'
    outputDirectory = "$buildDir/javadoc"
    // processConfigurations = ['compile', 'extra']
    // includes = ['packages.md', 'extra.md']
    //samples = ['samples/basic.kt', 'samples/advanced.kt']
    // linkMapping {
        // dir = "src/main/kotlin"
        // url = "https://github.com/dnene/hattip/"
        // suffix = "#L"
    // }
    //sourceDirs = files('src/main/kotlin')
}

bintray {
    user = BINTRAY_USER
    key = BINTRAY_API_KEY
    configurations = ['archives']
    publish = true
    pkg {
        repo = 'org.kotyle'
        name = 'hattip'
        desc = "Kotlin HTTP Client Libraries"
        userOrg = user
        licenses = ['Apache-2.0']
        vcsUrl = 'https://github.com/dnene/hattip.git'
        issueTrackerUrl = 'https://github.com/dnene/hattip/issues'
        labels = ['kotlin', 'http', 'client']
        publicDownloadNumbers = true
        version {
            name = "${project.version}"
            desc = "Very early release ${project.version}, under continuing development"
            vcsTag = "${project.version}"
        }
    }
}


task dokkaJavadoc(type: org.jetbrains.dokka.gradle.DokkaTask) {
    outputFormat = 'javadoc'
    outputDirectory = "$buildDir/javadoc"
}

dependencies {
    compile "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version",
            "ch.qos.logback:logback-classic:1.1.6",
            "org.kotyle:kylix:0.0.1"

    testCompile "junit:junit:${junit_version}",
            "org.nanohttpd:nanohttpd:2.2.0"
}

sourceSets {
  main.java.srcDirs += 'src/main/kotlin'
  test.java.srcDirs += 'src/test/kotlin'
}

